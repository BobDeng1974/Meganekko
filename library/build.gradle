apply plugin: 'com.android.library'

dependencies {
    /*
     * Oculus Mobile SDK integration
     */
    compile name: 'VrAppFramework', ext: 'aar'
    compile project(':VrAppSupport:SystemUtils:Projects:AndroidPrebuilt')
    compile project(':VrAppSupport:VrGUI:Projects:AndroidPrebuilt')
    compile project(':VrAppSupport:VrLocale:Projects:AndroidPrebuilt')
    compile project(':VrAppSupport:VrSound:Projects:AndroidPrebuilt')

    /*
     * Dependencies
     */
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.android.support:support-v4:23.1.0'
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    buildTypes {
        debug {
            jniDebuggable true
        }
    }

    sourceSets {
        main {
            jniLibs.srcDir 'src/main/libs'
            jni.srcDirs = [] //disable automatic ndk-build call
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

/*
 * NDK integration
 */

project.afterEvaluate {
    compileDebugNdk.dependsOn 'NDKBuildDebugMGN'
    compileReleaseNdk.dependsOn 'NDKBuildReleaseMGN'
    clean.dependsOn 'NDKBuildCleanMGN'
}

task NDKBuildDebugMGN(type: Exec) {
    commandLine GetNDKBuildCmd(), 'V=0', '-j10', 'NDK_DEBUG=1', 'OVR_DEBUG=1', "NDK_PROJECT_PATH=$projectDir/src/main"
}

task NDKBuildReleaseMGN(type: Exec) {
    commandLine GetNDKBuildCmd(), 'V=0', '-j10', 'NDK_DEBUG=0', 'OVR_DEBUG=0', "NDK_PROJECT_PATH=$projectDir/src/main"
}

task NDKBuildCleanMGN(type: Exec) {
    commandLine GetNDKBuildCmd(), 'clean', "NDK_PROJECT_PATH=$projectDir/src/main"
}

/*
 * Generate distribution package
 */

apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            def deployPath = file('./repository')
            repository(url: "file://${deployPath.absolutePath}")
            pom.version = '1.0.0'
            pom.groupId = 'com.eje_c'
            pom.artifactId = 'meganekko'
        }
    }
}
