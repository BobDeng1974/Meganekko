import org.gradle.internal.os.OperatingSystem

// Top-level build file where you can add configuration options common to all sub-projects/modules.

def ovrMobile = System.getenv('OVR_SDK_MOBILE')

if (!ovrMobile) {
    ovrMobile = "../../ovr_sdk_mobile_1.0.0.0"
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
    }
}

allprojects {
    repositories {

        flatDir {
            // Add paths to prebuilt libs to all our subprojects.
            dir "$ovrMobile/VrApi/Libs/Android"
            dir "$ovrMobile/VrAppFramework/Libs/Android"
            dir "$ovrMobile/VrAppSupport/SystemUtils/Libs/Android"
        }

        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def GetNDKBuildCmd() {

    def ndkDir = System.getenv('ANDROID_NDK_HOME')
    if (ndkDir == null) {
        ndkDir = System.getenv('ANDROID_NDK')
    }
    if (ndkDir == null) {
        ndkDir = System.getenv('NDKROOT')
    }
    if (ndkDir == null) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        ndkDir = properties.getProperty('ndk.dir')
    }
    if (ndkDir == null) {
        throw new GradleException('NDK not found! Check your environment for ANDROID_NDK or your local.properties contains ndk.dir')
    }
    if (OperatingSystem.current().isWindows()) {
        return ndkDir + '/ndk-build.cmd'
    } else {
        return ndkDir + '/ndk-build'
    }
}
